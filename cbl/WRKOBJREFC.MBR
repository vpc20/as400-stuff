       IDENTIFICATION DIVISION.
       PROGRAM-ID. WRKOBJREFC.
       AUTHOR. VPC.

      *===============================================================*
      * Subfile Program Template                                      *
      *                                                               *
      *---------------------------------------------------------------*
      * Amendment History                                             *
      *                                                               *
      * Date      Pgmr  Description                                   *
      * --------  ---   --------------------------------------------- *
      *                                                               *
      *===============================================================*

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.  IBM-AS400.
       OBJECT-COMPUTER.  IBM-AS400.

      *===============================================================*

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

           SELECT PGMREFPF         ASSIGN TO DATABASE-PGMREFPF
                  ORGANIZATION     IS INDEXED
                  ACCESS           IS DYNAMIC
                  RECORD KEY       IS EXTERNALLY-DESCRIBED-KEY.

           SELECT WRKOBJREFD        ASSIGN TO WORKSTATION-WRKOBJREFD-SI
                  ORGANIZATION     IS TRANSACTION
                  ACCESS MODE      IS DYNAMIC
                  RELATIVE KEY     IS WS-SFL-RECNUM
                  CONTROL-AREA     IS WS-CONTROL-AREA.

      *===============================================================*

       DATA DIVISION.

       FILE SECTION.
       FD  PGMREFPF
           LABEL RECORDS ARE STANDARD.
       01  PGMREFPF-REC.
           COPY DDS-ALL-FORMATS OF PGMREFPF.

       FD  WRKOBJREFD
           LABEL RECORDS ARE STANDARD.
       01  WRKOBJREFDREC   PIC X(1920).


      *===============================================================*

       WORKING-STORAGE SECTION.

      *--- Display file records
       01  WS-SFLREC1-I.
           COPY DDS-SFLREC1-I OF WRKOBJREFD
                REPLACING SFLREC1 BY SFLREC1-I.
       01  WS-SFLREC1-O.
           COPY DDS-SFLREC1-O OF WRKOBJREFD
                REPLACING SFLREC1 BY SFLREC1-O.

       01  WS-SFLCTL1-I.
           COPY DDS-SFLCTL1-I OF WRKOBJREFD.
       01  WS-SFLCTL1-O.
           COPY DDS-SFLCTL1-O OF WRKOBJREFD.

      *01  WS-FKEY1-O.
      *    COPY DDS-FKEY1-O   OF WRKOBJREFD.

      *--- Display file indicators
       01  INDIC-AREA.
           COPY DDS-ALL-FORMATS-INDIC OF WRKOBJREFD.

      *--- Display file control area
       01  WS-CONTROL-AREA.
           05 FKEY-PRESSED             PIC X(2).
              88 F0-ENTER              VALUE "00".
              88 F3-EXIT               VALUE "03".
              88 F6-ADD                VALUE "06".
              88 F90-PAGEDOWN          VALUE "90".
              88 F91-PAGEUP            VALUE "91".

       01  PROGRAM-SWITCHES.
           05 END-PROGRAM-SW           PIC 9.
              88 START-PROGRAM         VALUE 0.
              88 END-PROGRAM           VALUE 1.
           05 PGMREFPF-EOF-SW         PIC 9.
              88 NOT-EOF-PGMREFPF     VALUE 0.
              88 EOF-PGMREFPF         VALUE 1.
           05 SFLREC-FOUND-SW          PIC 9.
              88 SFLREC-NOT-FOUND      VALUE 0.
              88 SFLREC-FOUND          VALUE 1.

       01  WS-WORK-AREAS.
           05 WS-OPT                   PIC 9  VALUE ZEROES.
              88 DSPPGM-OPTION         VALUE 5.
              88 DSPPGMREF-OPTION      VALUE 6.
              88 WRKOBJ-OPTION         VALUE 7.
           05 WS-SFL-RECNUM            PIC 9(4)  VALUE ZEROES.
           05 WS-SFLSIZ                PIC 9(4)  VALUE ZEROES.
           05 WS-SFLRCDNBR             PIC 9(4)  VALUE ZEROES.
           05 WS-CTR                   PIC 9(4)  VALUE ZEROES.
           05 WS-PREV-WHFNAM           PIC X(10) VALUE SPACES.
      *    05 WS-PREV-POSPNAM          PIC X(10) VALUE SPACES.

       01  WS-QCMDEXC-PARM.
           05 WS-CMD.
              10 WS-CMD1               PIC X(64) VALUE SPACES.
           05 WS-CMD-LEN               PIC 9(10)V9(5) COMP-3 VALUE 60.

      *01  PARMHDRCB2-PARM.
      *    05 PARMHDRCB2-PARMCODE          PIC X(10).
      *    05 PARMHDRCB2-MAINT-MODE        PIC X(1).
      *    05 PARMHDRCB2-MSGCODE           PIC X(6).
      *
      *01  PARMDTLCB1-PARM.
      *    05 PARMDTLCB1-PARMCODE          PIC X(10).
      *    05 PARMDTLCB1-MSGCODE           PIC X(6).

      *===============================================================*

       PROCEDURE DIVISION.

       0000-MAIN.

           PERFORM 1000-INITIAL.
           PERFORM 2000-PROCESS UNTIL END-PROGRAM.
           PERFORM 9000-CLEAN-UP.
           GOBACK.

      *===============================================================*

       1000-INITIAL.

           OPEN INPUT PGMREFPF
                I-O   WRKOBJREFD.

           INITIALIZE SFLREC1-I
                      SFLREC1-O
                      SFLCTL1-I
                      SFLCTL1-O.

           MOVE 12 TO WS-SFLSIZ.
           MOVE 1 TO WS-SFLRCDNBR.

           SET START-PROGRAM TO TRUE.
           MOVE SPACES TO WHFNAM OF PGMREFPF-REC
                          WHPNAM OF PGMREFPF-REC.
           PERFORM 2900-LOAD-SUBFILE.

      *===============================================================*

       2000-PROCESS.

           WRITE WRKOBJREFDREC
              FORMAT "FKEY1"    INDICATORS INDIC-AREA.
           MOVE CORR SFLCTL1-I  TO SFLCTL1-O.
           WRITE WRKOBJREFDREC  FROM SFLCTL1-O
              FORMAT "SFLCTL1"  INDICATORS INDIC-AREA.
           READ  WRKOBJREFD INTO SFLCTL1-I
              FORMAT "SFLCTL1"  INDICATORS INDIC-AREA.
           MOVE B"0" TO IN31.

           EVALUATE TRUE

              WHEN F0-ENTER
                 IF WHFNAM OF SFLCTL1-I  NOT = WS-PREV-WHFNAM
                 OR POSPNAM OF SFLCTL1-I NOT = SPACES
                    MOVE 1 TO WS-SFLRCDNBR
                    MOVE WHFNAM OF SFLCTL1-I  TO WHFNAM OF PGMREFPF-REC
                                                 WS-PREV-WHFNAM
                    MOVE POSPNAM OF SFLCTL1-I TO WHPNAM OF PGMREFPF-REC
      *                                          WS-PREV-POSPNAM
                    PERFORM 2900-LOAD-SUBFILE
                 ELSE
                    IF WS-SFL-RECNUM > 0
                       SET SFLREC-FOUND  TO TRUE
                       MOVE ZEROES TO WS-SFL-RECNUM
                       PERFORM 3000-PROCESS-SUBFILE
                          UNTIL SFLREC-NOT-FOUND
                       PERFORM 2860-READ-FIRST-SFLREC
                       PERFORM 2900-LOAD-SUBFILE
                    END-IF
                 END-IF

              WHEN F3-EXIT
                 SET END-PROGRAM TO TRUE

              WHEN F90-PAGEDOWN
                 PERFORM 2800-PAGEDOWN

              WHEN F91-PAGEUP
                 IF WS-SFL-RECNUM > 0
                    PERFORM 2850-PAGEUP
                 END-IF

           END-EVALUATE.

      *===============================================================*

       2800-PAGEDOWN.

           IF IN70 = B"1"
              MOVE 1 TO WS-SFLRCDNBR
              PERFORM 2900-LOAD-SUBFILE
           END-IF.

      *===============================================================*

       2850-PAGEUP.

           MOVE ZEROES TO WS-CTR.
           SET NOT-EOF-PGMREFPF TO TRUE.

           PERFORM 2860-READ-FIRST-SFLREC.
           START PGMREFPF
              KEY >= EXTERNALLY-DESCRIBED-KEY
              INVALID KEY
                 SET EOF-PGMREFPF TO TRUE
           END-START.

           PERFORM UNTIL EOF-PGMREFPF OR WS-CTR = WS-SFLSIZ
              READ PGMREFPF PRIOR RECORD
                 AT END
                    SET EOF-PGMREFPF TO TRUE
                    INITIALIZE QWHDRPPR
              END-READ
              IF NOT-EOF-PGMREFPF
                 IF WHFNAM OF PGMREFPF-REC = WHFNAM OF SFLCTL1-I
                    ADD 1 TO WS-CTR
                 ELSE
                    SET EOF-PGMREFPF TO TRUE
                    READ PGMREFPF NEXT RECORD
                 END-IF
              END-IF
           END-PERFORM.

           MOVE 1 TO WS-SFLRCDNBR.
           PERFORM 2900-LOAD-SUBFILE.

      *===============================================================*

       2860-READ-FIRST-SFLREC.

           MOVE WHFNAM OF SFLCTL1-I TO WHFNAM OF PGMREFPF-REC.

           MOVE 1 TO WS-SFL-RECNUM.
           READ SUBFILE WRKOBJREFD INTO SFLREC1-O
              FORMAT "SFLREC1"  INDICATORS INDIC-AREA.
           MOVE WHPNAM OF SFLREC1-O TO WHPNAM OF PGMREFPF-REC.

      *===============================================================*

       2900-LOAD-SUBFILE.

           MOVE WS-SFLRCDNBR TO SFLRCDNBR OF SFLCTL1-I.
           MOVE B"1" TO IN70.

      *--- Clear subfile
           MOVE B"0" TO IN81.
           WRITE WRKOBJREFDREC
              FORMAT "SFLCTL1"  INDICATORS INDIC-AREA.
           MOVE B"1" TO IN81.

      *--- Load subfile
           MOVE ZEROES TO WS-SFL-RECNUM.
           MOVE B"0" TO IN80.
           SET NOT-EOF-PGMREFPF TO TRUE.

           START PGMREFPF
              KEY >= EXTERNALLY-DESCRIBED-KEY
              INVALID KEY
                 SET EOF-PGMREFPF TO TRUE
              NOT INVALID KEY
                 READ PGMREFPF NEXT RECORD
                    AT END
                       SET EOF-PGMREFPF TO TRUE
                    NOT AT END
                       IF WHFNAM OF PGMREFPF-REC = WHFNAM OF SFLCTL1-I
                          READ PGMREFPF PRIOR RECORD
                             AT END
                               MOVE SPACES TO WHFNAM OF PGMREFPF-REC
                               START PGMREFPF
                                  KEY >= EXTERNALLY-DESCRIBED-KEY
                          END-READ
                       ELSE
                          SET EOF-PGMREFPF TO TRUE
                       END-IF
                 END-READ
           END-START.

           PERFORM UNTIL EOF-PGMREFPF OR WS-SFL-RECNUM = WS-SFLSIZ
              READ PGMREFPF NEXT RECORD
                 AT END
                    SET EOF-PGMREFPF TO TRUE
              END-READ
              IF NOT-EOF-PGMREFPF
                 IF WHFNAM OF PGMREFPF-REC = WHFNAM OF SFLCTL1-I
                    INITIALIZE SFLREC1-O
                    ADD 1 TO WS-SFL-RECNUM
                    MOVE CORR QWHDRPPR TO SFLREC1-O
                    WRITE SUBFILE WRKOBJREFDREC FROM SFLREC1-O
                       FORMAT "SFLREC1"  INDICATORS INDIC-AREA
                 ELSE
                    SET EOF-PGMREFPF TO TRUE
                 END-IF
              END-IF
           END-PERFORM.

      *--- Check if there are more records
      *--- IN70 is on  - display 'More...'
      *--- IN70 is off - display 'Bottom'
           IF NOT-EOF-PGMREFPF
              READ PGMREFPF NEXT RECORD
                 AT END
                    MOVE B"0" TO IN70
                 NOT AT END
                    MOVE B"1" TO IN70
              END-READ
           ELSE
              MOVE B"0" TO IN70
           END-IF.

      *--- Turn on SFLDSP indicator if subfile record is created
           IF WS-SFL-RECNUM > 0
              MOVE B"1" TO IN80
           END-IF.

      *    IF WS-SFL-RECNUM > WS-SFLRCDNBR
      *       MOVE WS-SFL-RECNUM TO SFLRCDNBR OF SFLCTL1-I
      *    END-IF.

      *===============================================================*

       3000-PROCESS-SUBFILE.

           ADD 1 TO WS-SFL-RECNUM.
           READ SUBFILE WRKOBJREFD INTO SFLREC1-O
              FORMAT "SFLREC1"  INDICATORS INDIC-AREA
              INVALID KEY
                 SET SFLREC-NOT-FOUND TO TRUE
           END-READ.

           IF SFLREC-FOUND
              IF OPT OF SFLREC1-O NOT = ZEROES
                 MOVE OPT OF SFLREC1-O TO WS-OPT
                 EVALUATE TRUE
                    WHEN DSPPGM-OPTION
                       IF WHSPKG OF SFLREC1-O = "Q"
                          MOVE B"1" TO IN31
                       ELSE
                          MOVE SPACES TO WS-CMD
                          STRING "DSPPGM "           DELIMITED BY SIZE
                                 WHLIB  OF SFLREC1-O DELIMITED BY " "
                                 "/"                 DELIMITED BY SIZE
                                 WHPNAM OF SFLREC1-O DELIMITED BY " "
                                 INTO WS-CMD
                          MOVE 28 TO WS-CMD-LEN
                          CALL "QCMDEXC" USING WS-CMD WS-CMD-LEN
                       END-IF
                    WHEN DSPPGMREF-OPTION
                       MOVE SPACES TO WS-CMD
                       STRING "DSPPGMREF "        DELIMITED BY SIZE
                              WHLIB  OF SFLREC1-O DELIMITED BY " "
                              "/"                 DELIMITED BY SIZE
                              WHPNAM OF SFLREC1-O DELIMITED BY " "
                              " OBJTYPE(*ALL)"    DELIMITED BY SIZE
                              INTO WS-CMD
                       MOVE 45 TO WS-CMD-LEN
                       CALL "QCMDEXC" USING WS-CMD WS-CMD-LEN
                    WHEN WRKOBJ-OPTION
                       MOVE SPACES TO WS-CMD
                       STRING "WRKOBJ "           DELIMITED BY SIZE
                              WHLIB  OF SFLREC1-O DELIMITED BY " "
                              "/"                 DELIMITED BY SIZE
                              WHPNAM OF SFLREC1-O DELIMITED BY " "
                              INTO WS-CMD
                       MOVE 28 TO WS-CMD-LEN
                       CALL "QCMDEXC" USING WS-CMD WS-CMD-LEN
                 END-EVALUATE
                 MOVE WS-SFL-RECNUM TO WS-SFLRCDNBR
              END-IF

              MOVE ZEROES TO OPT OF SFLREC1-O
              REWRITE SUBFILE WRKOBJREFDREC FROM SFLREC1-O
                 FORMAT "SFLREC1"  INDICATORS INDIC-AREA
           END-IF.

      *===============================================================*

      *3100-CALL-PARMHDRCB2.
      *
      *    INITIALIZE PARMHDRCB2-PARM.
      *    IF NOT ADD-OPTION
      *       MOVE PARMCODE OF SFLREC1-O
      *         TO PARMHDRCB2-PARMCODE OF PARMHDRCB2-PARM
      *    END-IF
      *    EVALUATE TRUE
      *       WHEN ADD-OPTION
      *          MOVE "A" TO PARMHDRCB2-MAINT-MODE
      *       WHEN UPDATE-OPTION
      *          MOVE "U" TO PARMHDRCB2-MAINT-MODE
      *       WHEN DELETE-OPTION
      *          MOVE "D" TO PARMHDRCB2-MAINT-MODE
      *       WHEN INQUIRE-OPTION
      *          MOVE "I" TO PARMHDRCB2-MAINT-MODE
      *    END-EVALUATE.
      *
      *    CALL "PARMHDRCB2" USING PARMHDRCB2-PARM.
      *
********** IF  DELETE-OPTION
********** AND PARMHDRCB2-MSGCODE = "M00003"
**********     SET DELETE-OK TO TRUE
********** END-IF.

      *===============================================================*

       9000-CLEAN-UP.

           CLOSE PGMREFPF
                 WRKOBJREFD.

