FCIHPTRCD  UF A E           K DISK                                     
FCIHS05X   CF   E             WORKSTN                                  
***--------------------------------------------------------------------
DMMode            S              7A                                    
DValid_Data       S               N                                    
***--------------------------------------------------------------------
C     KPTRCD        KList                                              
C                   KFld                    CHANNEL                    
C                   KFld                    TRANCODE                   
***--------------------------------------------------------------------
*** Main Routine                                                       
***--------------------------------------------------------------------
C                   Eval      MAINTMODE = 'Display Record'             
C                   Eval      MMode = 'Display'                        
***--- Main Loop                                                       
C                   DOU       *In03                                    
C                   EXFMT     CIHS05REC1                               
C                   Eval      *In30 = *Off                             
C                   Eval      *In31 = *Off                             
C                   Eval      *In32 = *Off
C                   Eval      *In33 = *Off                    
***                                                           
C                   SELECT                                    
***--- F3=Exit                                                
C                   WHEN      *In03                           
C                   ITER                                      
***--- F6=Add                                                 
C                   WHEN      *In06                           
C                   Eval      MAINTMODE = 'Add Record'        
C                   Eval      MMode = 'Add'                   
***--- F7=Edit                                                
C                   WHEN      *In07                           
C                   Eval      MAINTMODE = 'Edit Record'       
C                   Eval      MMode = 'Edit'                  
***--- F8=Delete                                              
C                   WHEN      *In08                           
C                   Eval      MAINTMODE = 'Delete Record'     
C                   Eval      MMode = 'Delete'                
***--- F9=Display                                             
C                   WHEN      *In09                           
C                   Eval      MAINTMODE = 'Display Record'
C                   Eval      MMode = 'Display'                    
***--- Enter Key                                                   
C                   OTHER                                          
C                   EXSR      Validate_KFld                        
C                   If        Valid_Data                           
C                   EXSR      ProcessDtl                           
C                   EndIf                                          
C                   ENDSL                                          
***                                                                
C                   ENDDO                                          
C                   SETON                                        LR
***----------------------------------------------------------------
*** Validate key fields                                            
***----------------------------------------------------------------
C     Validate_Kfld BEGSR                                          
***                                                                
C                   Eval      Valid_Data = *On                     
C                   If        channel = *blanks                    
C                   Eval      *In32 = *On                          
C                   Eval      Valid_Data = *Off                    
C                   EndIf
C                   If        trancode = *blanks                    
C                   Eval      *In33 = *On                           
C                   Eval      Valid_Data = *Off                     
C                   EndIf                                           
***                                                                 
C                   If        Valid_Data                            
C     KPTRCD        CHAIN     CIHPTRCD                              
C                   SELECT                                          
***                                                                 
C                   WHEN      MMode = 'Add'                         
C                   If        %Found                                
C                   Eval      *In30 = *On                           
C                   Eval      Valid_Data = *Off                     
C                   else                                            
C                   eval      SHRTNAME = *blanks                    
C                   eval      TRANDESC = *blanks                    
C                   EndIf                                           
***                                                                 
C                   WHEN      MMode = 'Edit' or MMode = 'Delete' or 
C                             MMode = 'Display'                     
C                   If        NOT %Found
C                   Eval      *In31 = *On                              
C                   Eval      Valid_Data = *Off                        
C                   else                                               
C                   if        MMode = 'Display'                        
C                   Eval      *In60 = *On                              
C                   else                                               
C                   Eval      *In60 = *Off                             
C                   endif                                              
C                   EndIf                                              
***                                                                    
C                   ENDSL                                              
C                   EndIf                                              
***                                                                    
C                   ENDSR                                              
***--------------------------------------------------------------------
*** Process Details                                                    
***--------------------------------------------------------------------
C     ProcessDtl    BEGSR                                              
***                                                                    
C                   do                                                 
C                   exfmt     cihs05rec2
C                   if        *in12                                    
C                   leave                                              
C                   endif                                              
***                                                                    
C                   if        MMode = 'Display'                        
C                   leave                                              
C                   else                                               
C                   exsr      ValidateDtl                              
C                   if        Valid_Data                               
C                   exsr      UpdateTran                               
C                   leave                                              
C                   endif                                              
C                   endif                                              
C                   enddo                                              
***                                                                    
C                   ENDSR                                              
***--------------------------------------------------------------------
*** Validate Details                                                   
***--------------------------------------------------------------------
C     ValidateDtl   BEGSR                                              
***
C                   Eval      Valid_Data = *On                         
***                                                                    
C                   ENDSR                                              
***--------------------------------------------------------------------
*** Update transaction codes file                                      
***--------------------------------------------------------------------
C     UpdateTran    BEGSR                                              
***                                                                    
C                   select                                             
***                                                                    
C                   when      MMode = 'Add'                            
C                   write     trncdrec                                 
***                                                                    
C                   when      MMode = 'Edit'                           
C                   update    trncdrec                                 
***                                                                    
C                   when      MMode = 'Delete'                         
C                   delete    cihptrcd                                 
***                                                                    
C                   endsl                                              
***
C                   ENDSR


     A*%%TS  SD  20031218  114836  TMPCVP      REL-V5R2M0  5722-WDS    
     A*%%EC                                                            
     A                                      DSPSIZ(24 80 *DS3)         
     A                                      ERRSFL                     
     A          R CIHS05REC1                                           
     A*%%TS  SD  20031217  170023  TMPCVP      REL-V5R2M0  5722-WDS    
     A                                      CF03(03)                   
     A                                      CF06(06)                   
     A                                      CF07(07)                   
     A                                      CF08(08)                   
     A                                      CF09(09)                   
     A                                  1  2'CIHMAN04X'                
     A                                  1 31'United Overseas Bank'     
     A                                      DSPATR(HI)                 
     A                                  1 71DATE(*JOB *YY)             
     A                                      EDTWRD('0  /  /    ')      
     A                                  2  2USER                       
     A                                  2 27'Transaction Code Maintenan
     A                                      COLOR(WHT)                 
     A                                  2 72TIME
     A                                      EDTWRD('0  :  :  ')        
     A            MAINTMODE     15A  O  4  2COLOR(BLU)                 
     A                                  6  2'Channel'                  
     A            CHANNEL        5A  B  6 20                           
     A  30                                  ERRMSG('Record already exis
     A  31                                  ERRMSG('Record does not exi
     A  32                                  ERRMSG('Channel should not 
     A                                      s.')                       
     A                                  7  2'Transaction Code'         
     A            TRANCODE      10A  B  7 20                           
     A  33                                  ERRMSG('Transaction code sh
     A                                       be blanks.')              
     A                                 23  2'F3=Exit   F6=Add   F7=Edit
     A                                      lete   F9=Display'         
     A                                      COLOR(BLU)                 
     A          R CIHS05REC2                                           
     A*%%TS  SD  20031218  114836  TMPCVP      REL-V5R2M0  5722-WDS    
     A                                      CF12(12)                   
     A                                  1  2'CIHMAN04X'                
     A                                  1 31'United Overseas Bank'     
     A                                      DSPATR(HI)
     A                                  1 71DATE(*JOB *YY)             
     A                                      EDTWRD('0  /  /    ')      
     A                                  2  2USER                       
     A                                  2 27'Transaction Code Maintenan
     A                                      COLOR(WHT)                 
     A                                  2 72TIME                       
     A                                      EDTWRD('0  :  :  ')        
     A            MAINTMODE     15A  O  4  2COLOR(BLU)                 
     A                                  6  2'Channel'                  
     A            CHANNEL        5A  O  6 20                           
     A                                  7  2'Transaction Code'         
     A            TRANCODE      10A  O  7 20                           
     A                                  8  2'Short Name'               
     A            SHRTNAME      10A  B  8 20                           
     A  60                                  DSPATR(PR)                 
     A N60                                  DSPATR(UL)                 
     A                                  9  2'Description'              
     A            TRANDESC      40A  B  9 20CHECK(LC)                  
     A  60                                  DSPATR(PR)                 
     A N60                                  DSPATR(UL)                 
     A                                 23  2'F12=Cancel'
     A                                      COLOR(BLU)

CIHMAN04X                    United Overseas Bank                    20/03/1218
TMPCVP                   Transaction Code Maintenance                 14:13:01 
                                                                               
Display Record                                                                 
                                                                               
Channel                                                                        
Transaction Code                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
F3=Exit   F6=Add   F7=Edit   F8=Delete   F9=Display

CIHMAN04X                    United Overseas Bank                    20/03/1218
TMPCVP                   Transaction Code Maintenance                 14:13:32 
                                                                               
Display Record                                                                 
                                                                               
Channel           ATM                                                          
Transaction Code  1010                                                         
Short Name        BAL ENQ                                                      
Description       Balance Enquiry                                              
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
                                                                               
F12=Cancel
