      *==============================================================*
      * Program name: CUSTRPG1                                       *
      * Purpose.....: Customer Selection Window Program              *
      *                                                              *
      * Indicators:                                                  *
      *     40  -  Clear subfile                                     *
      *     41  -  Display subfile                                   *
      *     50  -  Already at top of area                            *
      *     51  -  Already at bottom of area                         *
      *     97  -  Subfile record not found                          *
      *     98  -  EOF for read changed records in CSFL subfile      *
      *     99  -  EOF for CUSTLFNM                                  *
      *==============================================================*
      *
      *==============================================================*
      * Files Declaration                                            *
      *==============================================================*
      *
     FCUSTLFNMIF  E           K        DISK
     FCUSTDSP1CF  E                    WORKSTN      KINFDS INFDS
     F                                        CRRN  KSFILE CSFL
      *
      *==============================================================*
      * Data Structures                                              *
      *==============================================================*
      *
     IINFDS       DS
     I                                      369 369 KEY
      *
      *==============================================================*
      * Constants Declaration                                        *
      *==============================================================*
      *
      * Function keys
     I              X'33'                 C         F03
     I              X'3C'                 C         F12
     I              X'F4'                 C         PAGEUP
     I              X'F5'                 C         PAGEDN
     I              X'F1'                 C         ENTER
      *
      *==============================================================*
      * Input parameter I#CUST not used in program                   *
      * Output parameter O#CUST is selected CUSTNO (Customer number) *
      *==============================================================*
      *
     C           *ENTRY    PLIST
     C                     PARM O#CUST    I#CUST
      *
      * Position record pointer at the beginning of file
     C           *LOVAL    SETLLCUSTLFNM
      *
      * Build window
     C                     WRITECSFLCMD
      * Fill subfile
     C                     EXSR @LOAD
      *
      *==============================================================*
      * Main Loop                                                    *
      *==============================================================*
      *
      * Display selection window and accept user input
     C           KEY       DOUEQF03
     C           KEY       OREQ F12
      *
     C                     WRITECSFLCMD
     C                     EXFMTCSFLCTL
      *
      * Check user input
     C                     SELEC
      *
     C           KEY       WHEQ ENTER
     C           NCNAME    IFEQ *BLANKS
     C                     EXSR @CHKSL
     C                     ELSE
     C                     EXSR @REPOS
     C                     EXSR @LOAD
     C                     ENDIF
      *
     C           KEY       WHEQ PAGEUP
     C                     EXSR @PGUP
      *
     C           KEY       WHEQ PAGEDN
     C                     EXSR @PGDN
      *
     C                     ENDSL
      *
     C                     ENDDO
      *
      * End of program
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *==============================================================*
      * Check if user made a selection                               *
      *==============================================================*
      *
     C           @CHKSL    BEGSR
      *
     C                     READCCSFL                     98
      *
     C           *IN98     DOWEQ*OFF
     C           SELIO     IFEQ 1
     C                     Z-ADDCUSTNO    O#CUST
     C                     MOVE F03       KEY
     C                     LEAVE
     C                     ENDIF
     C                     READCCSFL                     98
     C                     ENDDO
      *
     C                     ENDSR
      *
      *==============================================================*
      * Reposition subfile for "Position to" entry                   *
      *==============================================================*
      *
     C           @REPOS    BEGSR
      *
      * Position file by partial entry NCNAME in dspf
     C                     MOVE *OFF      *IN50
     C                     MOVE *OFF      *IN51
      *
      * Position file by partial entry NCNAME in dspf
      * If position value is greater than last record in file,
      * position pointer to last record
     C                     MOVELNCNAME    CNAME
     C           CNAME     SETLLCUSTLFNM             96
     C           *IN96     IFEQ *ON
     C           *HIVAL    SETLLCUSTLFNM
     C                     READPCUSTLFNM                 96
     C                     READPCUSTLFNM                 96
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Page Up                                                      *
      *==============================================================*
      *
     C           @PGUP     BEGSR
      *
     C                     MOVE *OFF      *IN51
     C                     Z-ADD0         CRRN
      *
      *  Posiotion pointer using first record in subfile
     C           1         CHAINCSFL                 97
     C           CNAME     SETLLCUSTLFNM
      *
      *  Read previous 11 records
     C                     DO   11
     C                     READPCUSTLFNM                 99
     C           *IN99     IFEQ *ON
     C           *LOVAL    SETLLCUSTLFNM
     C                     LEAVE
     C                     ENDIF
     C                     ADD  1         CRRN
     C                     ENDDO
      *
      *  Already at top of area
     C           CRRN      IFEQ 1
     C                     MOVE *ON       *IN50
     C                     ENDIF
      *
     C                     EXSR @LOAD
      *
     C                     ENDSR
      *
      *==============================================================*
      * Page Down                                                    *
      *==============================================================*
      *
     C           @PGDN     BEGSR
      *
     C                     MOVE *OFF      *IN50
      *
      * If not yet at bottom of file, reload next 10 records
     C           *IN42     IFEQ *ON
     C                     MOVE *ON       *IN51
     C                     ELSE
     C                     READPCUSTLFNM                 97
     C                     EXSR @LOAD
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Load subfile records forward                                 *
      *==============================================================*
      *
     C           @LOAD     BEGSR
      *
      * Clear Subfile
     C                     EXSR @CLSFL
      *
      * Initialize subfile relative record number
     C                     Z-ADD0         CRRN    50
     C                     MOVE *OFF      *IN42
      *
      *  Load 10 records in subfile
     C                     DO   10
     C                     READ CUSTLFNM                 99
     C           *IN99     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C                     ADD  1         CRRN
     C                     Z-ADD0         SELIO
     C                     WRITECSFL
     C                     ENDDO
      *
      *  One more read to determine if last record has been reached
      *  If las record has been read, position pointer to last record
     C                     READ CUSTLFNM                 99
     C           *IN99     IFEQ *ON
     C                     MOVE *ON       *IN42
     C           *HIVAL    SETLLCUSTLFNM
     C                     ENDIF
      *
      * Check for existing subfile records to condition SFLDSP
     C           CRRN      IFGT 0
     C                     MOVE *ON       *IN41
     C                     ELSE
     C                     MOVE *OFF      *IN41
     C                     END
      *
     C                     ENDSR
      *
      *==============================================================*
      * Clear Subfile                                                *
      *==============================================================*
      *
     C           @CLSFL    BEGSR
      *
     C                     MOVE *ON       *IN40
     C                     WRITECSFLCTL
     C                     MOVE *OFF      *IN40
      *
     C                     ENDSR
      *
      *==============================================================*
      * Initial Routine                                              *
      *==============================================================*
      *
     C           *INZSR    BEGSR
      *
      * Variable declarations and initialization
     C                     Z-ADD1         RCDNBR
     C                     MOVE *OFF      *IN50
     C                     MOVE *OFF      *IN51
      *
     C           *LIKE     DEFN CUSTNO    I#CUST
     C           *LIKE     DEFN CUSTNO    O#CUST
      *
     C                     ENDSR
